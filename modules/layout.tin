#event {SESSION CONNECTED} {
  #split 0 12;
  #draw green rounded box -13 100 -3 142 $pwin[];
  #draw green rounded box -13 1 -3 100 $window[];
};



#nop #split 0 2 0 -110
#nop #map offset 1 121 -4 -1
#nop #map flag vtmap on

#FUNCTION {colscale}
{
     #switch {10 * %1 / %2}
     {
          #case {0}
          {
               #return <faa>
          };
          #case {1}
          {
               #return <fba>
          };
          #case {2}
          {
               #return <fca>
          };
          #case {3}
          {
               #return <fda>
          };
          #case {4}
          {
               #return <fea>
          };
          #case {5}
          {
               #return <ffa>
          };
          #case {6}
          {
               #return <efa>
          };
          #case {7}
          {
               #return <dfa>
          };
          #case {8}
          {
               #return <cfa>
          };
          #case {9}
          {
               #return <bfa>
          };
          #default
          {
               #return <afa>
          }
     }
}

#ACTION {H: %1/%2 S: %3/%4 E: %5/%6 EXP:%7(%8) $:%9 %10>}
{
     #variable hpcol @colscale{%1;%2};
     #variable mncol @colscale{%3;%4};
     #variable epcol @colscale{%5;%6};
     #format time %t {%H:%M};
     #format {prompt1} {H: %s%s<238>/<138>%s<099>} {$hpcol} {%1} {%2};
     #format {prompt2} {S: %s%s<238>/<138>%s<099>} {$mncol} {%3} {%4};
     #format {prompt3} {E: %s%s<238>/<138>%s<099>} {$epcol} {%5} {%6}
}
{1}

#nop H: [red][truehp]/[truemaxhp][reset] S: [cyan][truesp]/[truemaxsp][reset] E: [yellow][trueep]/[truemaxep][reset] EXP:[exp]([protoadv]) $:[cash] [status] [coords]>
#PROMPT {H: %1/%2 S: %3/%4 E: %5/%6 EXP:%7(%8) $:%9 %10>} {$prompt1  $prompt2  $prompt3 EXP:%7(%8) $:%9 %10 $TARGET:$TARGET_STATUS} {row -2} {}

#variable TARGET {NA}
#variable TARGET_STATUS {NA}

#nop #action {%1 is %2}


#VARIABLE {window}
{
     {1} {}
     {2} {}
     {3} {}
     {4} {}
     {5} {}
     {6} {}
     {7} {}
     {8} {}
     {9} {}
     {10} {}
}
#ACTION {^%1 tells you '%2}
{
     showtowin <138>%1 <000><448>tells<000><138> '%2;
}
#action {^You tell %* '%*} {
  showtowin <448>tell<000><138> %1 '%2;
}

#nop #action {<party>: %*} { showtowin <party>: %1;}

#action {^%w [%2]: %*} {
  showtowin <138>%1 <228>[<448>%2<000><228>]: <138>%3;
}

#ACTION {~%1 chats '%2}
{
     showtowin %1 chats '%2
}

#ALIAS {showtowin}
{
     #list window del 1;
     #list window ins -1 {%0};
     #draw green rounded box -13 1 -3 100 $window[];
}

#ALIAS {test}
{
     #nop #split 6 1;
     #nop #screen clear top;
     #showme <138>Bubba tells you 'hello';
     #showme <158>Pamela chats 'bye';
     #showme <168>Bobbo tells you 'bli bla blo';
     #showme you tell wef 'wefwef';
}


#nop ,---------------------------------------------------------------------------.
#nop | Partyname: sc                                        Kills made: 285      |
#nop | Members: 3  Creator: Zork                               Exp/min: 98912    |
#nop +-----------------------------------------------------------+---------------+
#nop | Zork           1,1  Fol O    Hp:100%  Sp:140%  Ep: 55%    |               |
#nop | Mmiao          1,2  Fol O    Hp: 95%  Sp:100%  Ep: 41%    | Exp: 4182396  |
#nop | Aerious        1,3  Lea O    Hp: 50%  Sp:100%  Ep: 52%    |               |
#nop `---------------------------------------------------------------------------'
#nop | Mmiao          1,2      O    Hp: 20%  Sp: 36%  Ep: 47%    | Exp: 5031911  |
#nop | Mmiao          -,-           Hp:101%  Sp: 46%  Ep:100%    | Exp: 508012   |


#action {| Partyname: %1%sKills made: %2%s|} {
  pwininit;
}

#alias {pwininit} {
  #var GNAME {};
  #var pwin {{1}{}{2}{}{3}{}{4}{}{5}{}{6}{}{7}{}{8}{}{9}{}};
}
#var pwin {{1}{}{2}{}{3}{}{4}{}{5}{}{6}{}{7}{}{8}{}{9}{}};

#function calrow {
  #math result {(%1-1)*3+%2};
};
#action {%1 moves to position %2,%3.} {
  #math row1 {@calrow{$GNAME[%1][0];$GNAME[%1][1]}};
  #var pwin[$row1] {};
  #var GNAME[%1][0] %2;
  #var GNAME[%1][1] %3;
  pwinupdate %1;
}
#action {%1 guides %2 to position %3,%4.} {
  #math row1 {@calrow{$GNAME[%2][0];$GNAME[%2][1]}};
  #var pwin[$row1] {};
  #var GNAME[%2][0] %3;
  #var GNAME[%2][1] %4;
  pwinupdate %2;
}
#action {%1 moves to %2,%3 and %4 moves to %5,%6.} {
  #nop #showme swap;
  #var GNAME[%1][0] %2;
  #var GNAME[%1][1] %3;
  #var GNAME[%4][0] %5;
  #var GNAME[%4][1] %6;
  pwinupdate %1;
  pwinupdate %4;
}
#action {%1 has left the party.} {
  #math row {@calrow{$GNAME[%1][0];$GNAME[%1][1]}};
  #var pwin[$row] {};
  #list GNAME delete %1;
  #draw green rounded box -13 100 -3 142 $pwin[];
}
#action {You leave the party.} {
  pwininit;
  #draw green rounded box -13 100 -3 142 $pwin[];
}

#action {^<party>: %1 >> HP:[%!*] %2%!{%} SP:[%!*] %3%!{%} EP:[%!*] %4%!{%}%*} {
  #nop #showme update;
  #var GNAME[%1][2] %2;
  #var GNAME[%1][3] %3;
  #var GNAME[%1][4] %4;
  pwinupdate %1;
}
#gag {^<party>: %1 >> HP:[%!*] %2%!{%} SP:[%!*] %3%!{%} EP:[%!*] %4%!{%}%*};

#action {^| {[A-Za-z]+}%!s{[0-9]},{[0-9]}%!*Hp:%!s%4%!{%}%!sSp:%!s%5%!{%}%!sEp:%!s%6%!{%}%!s| Exp: %7%!s|$} {
  #var GNAME[%1] {{0}{%2}{1}{%3}{2}{%4}{3}{%5}{4}{%6}};
  pwinupdate %1;
}
#action {^| {[A-Za-z]+}%!s{[0-9]},{[0-9]}%!*Hp:%!s%4%!{%}%!sSp:%!s%5%!{%}%!sEp:%!s%6%!{%}%!s|%!s|$} {
  #nop #showme %1&&%2&&%3&&%4&&%5&&%6&&%7;

  #var GNAME[%1] {{0}{%2}{1}{%3}{2}{%4}{3}{%5}{4}{%6}};
  pwinupdate %1;
}

#alias {pwinupdate} {
  #var r $GNAME[%1][0];
  #var c $GNAME[%1][1];
  #var HP  $GNAME[%1][2];
  #var SP  $GNAME[%1][3];
  #var EP  $GNAME[%1][4];
  #math {row} {@calrow{$r;$c}};
  #var pwin[$row] {<138>%1($r,$c): H:@colscale{$HP;100}$HP<138> S:@colscale{$SP;100}$SP<138> E:@colscale{$EP;100}$EP<238>};
  #draw green rounded box -13 100 -3 142 $pwin[];
}
